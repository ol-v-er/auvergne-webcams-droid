apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId 'fr.openium.auvergnewebcams'
        minSdkVersion 21
        targetSdkVersion 33

        versionCode 34
        versionName '1.4.4'

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
        }
    }

    signingConfigs {
        debug {
            storeFile file("keys/debug.keystore")
        }

        release {
            storeFile file('keys/release.keystore')
            storePassword 'AWCOpenium'
            keyAlias 'AuvergneWebCamsOpenium'
            keyPassword 'AWCOpenium'
        }
    }

    buildTypes {
        debug {
            // Optimization
            minifyEnabled false
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false

            // Allow debug and release on the same device
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    //Export room schema for easy migration
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    // Needed for tests purposes and for exoPlayer
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

kapt { useBuildCache = true }

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])

    // --- Kotlin ---
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // --- AndroidX ---
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // --- Play services ---
    implementation 'com.google.android.gms:play-services-maps:18.1.0'

    // --- Firebase ---
    implementation platform('com.google.firebase:firebase-bom:29.3.1')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // --- Openium Tools ---
    implementation 'fr.openium:kotlin-tools:1.0.14'
    implementation 'fr.openium:rx-tools:1.0.5'

    // --- KTX ---
    implementation 'androidx.core:core-ktx:1.9.0'

    // --- Kodein ---
    implementation 'org.kodein.di:kodein-di-generic-jvm:6.3.1'
    implementation 'org.kodein.di:kodein-di-framework-android-x:6.3.1'

    // --- Rx ---
    implementation 'io.reactivex.rxjava2:rxjava:2.2.15'
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.1.0'
    implementation 'com.jakewharton.rxbinding3:rxbinding-appcompat:3.1.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    // --- Retrofit ---
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    debugImplementation 'com.squareup.retrofit2:retrofit-mock:2.9.0'

    // --- Log ---
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // --- Test ---
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:core-ktx:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'

    // --- ARCH ---
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.5.1'

    // --- Room ---
    implementation 'androidx.room:room-runtime:2.5.0'
    kapt 'androidx.room:room-compiler:2.5.0'
    implementation 'androidx.room:room-rxjava2:2.5.0'

    // --- Specific lib of the app ---

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.12.0'

    // BigImageViewer
    implementation 'com.github.piasy:BigImageViewer:1.8.1'
    implementation 'com.github.piasy:GlideImageLoader:1.8.1'

    // ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.18.2'

    // Like button (Twitter)
    implementation 'com.github.jd-alexander:LikeButton:0.2.3'
}